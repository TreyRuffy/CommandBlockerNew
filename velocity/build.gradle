plugins {
  alias libs.plugins.blossom
}

apply plugin: "com.github.johnrengelman.shadow"

repositories {
  maven { url "https://repo.velocitypowered.com/releases/" }
}

dependencies {
  api projects.common
  annotationProcessor libs.velocity.api
  compileOnly libs.velocity.api
}

shadowJar {
  archiveFileName = "${rootProject.archives_base_name}-Velocity-${project.ext.FULL_VERSION}.jar"

  dependencies {
    include(dependency("me.treyruffy.commandblocker:.*"))
    include(dependency("org.spongepowered:configurate.*"))
    include(dependency("io.leangen.geantyref:.*"))
    include(dependency("net.kyori:event.*"))
  }

  relocate "org.spongepowered.configurate", "me.treyruffy.commandblocker.libs.configurate"
  relocate "io.leangen.geantyref", "me.treyruffy.commandblocker.libs.geantyref"
  relocate "net.kyori.event", "me.treyruffy.commandblocker.libs.event"
}

artifacts {
  archives shadowJar
}

blossom {
  replaceToken("@project-version@", project.ext.FULL_VERSION)
}

indra {
  javaVersions {
    target(11)
    minimumToolchain(16)
    testWith(11, 17, 18)
  }
}
