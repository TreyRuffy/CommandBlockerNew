import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'net.kyori.blossom' version '1.3.0'
	id 'org.spongepowered.gradle.plugin' version '2.0.0'
}

repositories {
	maven { url 'https://repo.spongepowered.org/maven' }
}

dependencies {
	implementation project(path: ':common', configuration: 'shadow')

	compileOnly('net.kyori:adventure-platform-spongeapi:4.0.0') {
		exclude(module: 'adventure-bom')
		exclude(module: 'adventure-api')
		exclude(module: 'adventure-nbt')
	}
}

shadowJar {
	archiveFileName = "CommandBlocker-Sponge-${project.ext.FULL_VERSION}.jar"

	dependencies {
		include(dependency('me.treyruffy.commandblocker:.*'))
	}

	relocate 'net.kyori', 'me.treyruffy.commandblocker.libs.adventure'
	relocate 'org.simpleyaml', 'me.treyruffy.commandblocker.libs.simpleyaml'
	relocate 'org.yaml', 'me.treyruffy.commandblocker.libs.yaml'
}

artifacts {
	archives shadowJar
}

blossom {
	replaceToken("@project-version@", project.ext.FULL_VERSION)
}

sponge {
	apiVersion("8.0.0-SNAPSHOT")
	license('GPLv3')
	loader {
		name(PluginLoaders.JAVA_PLAIN)
		version("1.0")
	}
	plugin("commandblocker") {
		displayName("CommandBlocker")
		entrypoint("me.treyruffy.commandblocker.sponge.CommandBlockerSponge")
		description("Command Blocker for Sponge")
		/*links {
			homepage("https://spongepowered.org")
			source("https://spongepowered.org/source")
			issues("https://spongepowered.org/issues")
		}*/
		contributor("TreyRuffy") {
			description("Lead Developer")
		}
		dependency("spongeapi") {
			loadOrder(PluginDependency.LoadOrder.AFTER)
			optional(false)
		}
	}
}