apply plugin: "com.github.johnrengelman.shadow"

repositories {
  maven {
    url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
  }
}

dependencies {
  api projects.common
  api libs.adventure.platform.bukkit
  compileOnly libs.paper.api
  implementation libs.commodore
  implementation libs.triumph.gui
  compileOnly libs.placeholderapi
}

shadowJar {
  archiveFileName = "${rootProject.archives_base_name}-Bukkit-${project.ext.FULL_VERSION}.jar"

  dependencies {
    include(dependency("me.treyruffy.commandblocker:.*"))
    include(dependency("me.lucko:commodore.*"))
    include(dependency("dev.triumphteam:.*"))
    include(dependency("net.kyori:.*"))
    include(dependency("org.spongepowered:.*"))
    include(dependency("io.leangen.geantyref:.*"))

    exclude(dependency("com.mojang:brigadier"))
  }

  relocate "net.kyori", "me.treyruffy.commandblocker.libs.kyori"
  relocate "dev.triumphteam.gui", "me.treyruffy.commandblocker.libs.triumphteamgui"
  relocate "org.spongepowered.configurate", "me.treyruffy.commandblocker.libs.spongepowered"
  relocate "io.leangen.geantyref", "me.treyruffy.commandblocker.libs.geantyref"
  relocate "me.lucko.commodore", "me.treyruffy.commandblocker.libs.commodore"
}

artifacts {
  archives shadowJar
}

processResources {
  filesMatching("plugin.yml") {
    expand "pluginVersion": project.ext.FULL_VERSION
  }
}
