plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'java'
}

repositories {
	maven {
		url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
	}
	maven { url = "https://repo.mattstudios.me/artifactory/public/" }
}

dependencies {
	implementation project(path: ':common', configuration: 'shadow')

	api ('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT') {
		exclude(module: 'adventure-bom')
		exclude(module: 'adventure-api')
		exclude(module: 'adventure-nbt')
	}

	implementation('net.kyori:adventure-platform-bukkit:4.1.0')
	implementation('dev.triumphteam:triumph-gui:3.1.2')
	compileOnly 'me.clip:placeholderapi:2.10.10'
}

shadowJar {
	archiveFileName = "CommandBlocker-Bukkit-${project.ext.FULL_VERSION}.jar"

	dependencies {
		include(dependency('me.treyruffy.commandblocker:.*'))
		include(dependency('net.kyori:.*'))
		include(dependency('dev.triumphteam:.*'))
	}

	relocate 'net.kyori', 'me.treyruffy.commandblocker.libs.adventure'
	relocate 'org.simpleyaml', 'me.treyruffy.commandblocker.libs.simpleyaml'
	relocate 'org.yaml', 'me.treyruffy.commandblocker.libs.yaml'
	relocate 'dev.triumphteam.gui', 'me.treyruffy.commandblocker.libs.triumphteamgui'
}

artifacts {
	archives shadowJar
}

processResources {
	filesMatching('plugin.yml') {
		expand 'pluginVersion': project.ext.FULL_VERSION
	}
}